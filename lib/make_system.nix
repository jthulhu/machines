{ self
, nixpkgs-stable
, home-manager
, nix-index-database
, ...
} @ inputs: config: { hostname
                    , system ? "x86_64-linux"
                    , overlays ? [ ]
                    , users ? [ "adri" ]
                    }: nixpkgs:
let
  inherit (builtins) listToAttrs;
  inherit (nixpkgs.lib) nixosSystem;
  custom = {
    my.hostname = hostname;
    nixpkgs.overlays = overlays;
    nix = {
      registry = {
        jpkgs = {
          from = {
            type = "indirect";
            id = "jpkgs";
          };
          flake = nixpkgs;
        };
        stable = {
          from = {
            type = "indirect";
            id = "stable";
          };
          flake = nixpkgs-stable;
        };
      };
    };
  };
  common = "${self}/system/configuration.nix";
  entrypoint = "${self}/system/hosts/${hostname}.nix";
  hardware = "${self}/system/hardware/${hostname}.nix";
  user-module = {
    home-manager = {
      useGlobalPkgs = true;

      users = listToAttrs (map (username: {
        name = username;
        value = {
          imports = [
            "${self}/home/hosts/${username}@${hostname}.nix"
            "${self}/home/home.nix"
          ];
          config.home = {
            inherit username;
            homeDirectory = "/home/${username}";
          };
        };
      }) users);
      
      sharedModules = [ nix-index-database.homeModules.default ];
    };
  };
in
nixosSystem {
  inherit system;
  modules = [
    common
    entrypoint
    hardware
    custom
    home-manager.nixosModules.home-manager
    user-module
  ];
}
